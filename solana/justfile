SOLANA_VERSION := "2.2.18"

# List all recipes
default:
	just --list --unsorted

# Build
build:
  GIT_REV=$(git rev-parse HEAD) cargo build-sbf -- --lib

# Compile
cargo-compile:
  cargo test -p testing --no-run --locked

# Test
test:
  cargo test --locked -p testing

# Download solana tool
download-solana:
	curl -L -o solana-release.tar.bz2 https://github.com/anza-xyz/agave/releases/download/v{{SOLANA_VERSION}}/solana-release-$(rustc -vV | sed -n 's|host: ||p').tar.bz2
	tar xjf solana-release.tar.bz2
	-rm -rf ~/.cargo/bin/perf-libs
	-rm -rf ~/.cargo/bin/platform-tools-sdk
	-rm -rf ~/.cargo/bin/deps
	mv solana-release/bin/* ~/.cargo/bin/
	rm solana-release.tar.bz2
	rm -rf solana-release

solana-test-validator:
    nohup solana-test-validator > /dev/null &
    @printf "Waiting for solana-test-validator to start..."
    @while ! curl -s http://localhost:8899 > /dev/null; do sleep 1; printf .; done; sleep 1; echo

# Cargo inherit
inherit:
    cargo autoinherit
